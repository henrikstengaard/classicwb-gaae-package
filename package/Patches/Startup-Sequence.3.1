;ClassicWB Startup-Sequence
;--------------------------
;GAAE
;Amiga OS 3.1

; set failat to 21
FailAt 21

; load scsi.device, if it exists
IF EXISTS DEVS:scsi.device
  ; load scsi.device, if current scsi.device is less than v43.45
  Version >NIL: "scsi.device" 43 45
  IF WARN
    C:LoadModule DEVS:scsi.device
  ENDIF
ENDIF

C:GetMouseInput LOCAL

IF $MouseInput EQ 1
  Unset MouseInput
  C:SetPatch QUIET
  C:Assign >NIL: ENV: RAM:
  C:Assign >NIL: T: RAM:
  C:Execute S:Maxmem-Sequence
  EndCLI >NIL:
EndIF

IF $MouseInput EQ 2
  Unset MouseInput
  C:Execute S:Bootmenu-Sequence
  EndCLI >NIL:
EndIF

Unset MouseInput

C:SetPatch QUIET

IF EXISTS C:StackAttack
  C:StackAttack
EndIF

Version >NIL:

IF EXISTS SYS:WBstartupD
  C:Rename >NIL: SYS:WBStartup SYS:WBStartupM
  C:Rename >NIL: SYS:WBStartupD SYS:WBStartup
EndIF

Resident >NIL: C:IconX PURE
Resident >NIL: C:WBRun PURE
Resident >NIL: C:Assign PURE
Resident >NIL: C:Execute PURE

IF EXISTS C:HDEnv
  C:MakeDir RAM:T RAM:Clipboards
  Assign >NIL: ENV: SYS:Command/ENV
  C:HDEnv
ELSE
  C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
  C:Copy >NIL: ENVARC: RAM:ENV ALL NOREQ
  Assign >NIL: ENV: RAM:ENV
EndIF

IF EXISTS C:PatchRAM
  C:PatchRAM
EndIF

IF EXISTS C:TagLiFE
  C:TagLiFE
EndIF

Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale

IF NOT EXISTS SYS:Fonts
  Assign FONTS:
EndIF

BindDrivers
C:Mount >NIL: DEVS:DOSDrivers/~(#?.info)

IF EXISTS DEVS:Monitors
  C:List >NIL: DEVS:Monitors/~(#?.info) TO T:M LFORMAT "DEVS:Monitors/%s"
  Execute T:M
  C:Delete >NIL: T:M
EndIF

SetEnv Workbench $Workbench
SetEnv Kickstart $Kickstart
UnSet Workbench
UnSet Kickstart

C:IPrefs
C:FPPrefs

Echo "Amiga Release 2.1.  Kickstart $Kickstart, Workbench $Workbench"
Echo ""
Echo "Green Amiga Alien Addition"

C:ConClip

Path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs SYS:WBStartup SYS:Tools SYS:Tools/Commodities

IF EXISTS S:User-Startup
  Execute S:User-Startup
EndIF

Resident Execute REMOVE
Resident Assign REMOVE

IF EXISTS C:StackAttack
  C:StackAttack AUTO
EndIF

C:LoadWB
EndCLI >NIL:
